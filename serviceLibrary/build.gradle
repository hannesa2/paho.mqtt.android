plugins {
    id "com.android.library"
    id "kotlin-android"
    id "maven-publish"
    id "com.google.devtools.ksp"
}

android {
    namespace "info.mqtt.android.service"
    testNamespace "info.mqtt.android.service.test"
    defaultConfig {
        minSdkVersion 21
        compileSdk rootProject.ext.compileSdkVersion
        targetSdkVersion rootProject.ext.targetSdk

        // Android Studio 4.1 doesn"t generate versionName in libraries any more
        // https://developer.android.com/studio/releases/gradle-plugin#version_properties_removed_from_buildconfig_class_in_library_projects
        buildConfigField "String", "VERSION_NAME", "\"${getTag()}\""

        testApplicationId "info.mgtt.android.service.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments useTestStorageService: "true"

        packagingOptions {
            pickFirst "META-INF/serviceLibrary_debug.kotlin_module"
        }

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas")
        }

        buildTypes {
            release {
                minifyEnabled false
                consumerProguardFile("proguard-sdk.pro")
            }
        }

        buildFeatures {
            buildConfig = true
        }
    }

    testFixtures {
        enable = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
}

dependencies {
    api "org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.core:core-ktx:1.15.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.1"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "androidx.work:work-runtime-ktx:2.10.0"
    implementation "com.github.AppDevNext.Logcat:LogcatCoreLib:$logcatVersion"

    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    androidTestImplementation "androidx.test.ext:junit-ktx:1.2.1"
    androidTestUtil "androidx.test.services:test-services:1.5.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.6.1"
    androidTestImplementation "androidx.test:rules:1.6.1"
}

publishing {
    publications {
        release(MavenPublication) {
            afterEvaluate {
                from components.release
            }
        }
    }
}