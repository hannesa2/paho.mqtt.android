plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId 'info.mqtt.android.extsample'
        minSdkVersion 21
        targetSdkVersion 34
        versionCode getGitCommitCount()
        versionName getTag()

        if (System.getenv("CI") == "true") { // Github action
            resValue "string", "add_connection_server_default", "10.0.2.2"
        } else
            resValue "string", "add_connection_server_default", "broker.hivemq.com"

        javaCompileOptions {
            annotationProcessorOptions { arguments = ["room.schemaLocation": "$projectDir/schemas".toString()] }
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments useTestStorageService: 'true'
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    namespace 'info.mqtt.android.extsample'
}

dependencies {
    implementation project(':serviceLibrary')

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation "androidx.core:core-ktx:1.10.1"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.github.AppDevNext.Logcat:LogcatCoreUI:$logcatVersion"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'com.github.AppDevNext:Moka:1.6'
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.5"
    androidTestUtil "androidx.test.services:test-services:1.4.2"
}
